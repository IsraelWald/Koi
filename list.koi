class ListNode {
    init(value) {
        this.value = value;
        this.next = nil;
        this.prev = nil;
    }
}

class List {
    init() {
        this.head = nil;
        this.tail = nil;
        this._length = 0;
    }
    append(item) {
        var node = ListNode(item);
        if (this.head == nil) {
            this.head = node;
            this.tail = node;
        } else {
            node.prev = this.tail;
            this.tail.next = node;
            this.tail = node;
        }
        this._length = this._length + 1;
        return this;
    }
    pop() {
        if (this.tail == nil) {
            return;
        }
        var node = this.tail;
        var prev = node.prev;
        this.tail = prev;
        if (prev != nil) {
            prev.next = nil;
        } else {
            this.head = nil;
        }
        this._length = this._length - 1;
        return node.value;
    }
    forEach(callback) {
        var node = this.head;
        while (node != nil) {
            if (callback(node.value)) {
                return;
            }
            node = node.next;
        }
    }
    get(index) {
        var node = this.head;
        var i = 0;
        while (node != nil) {
            if (i == index) {
                return node.value;
            }
            node = node.next;
            i = i + 1;
        }
        return nil;
    }
    len() {
        return this._length;
    }
    last() {
        if (this.tail != nil) {
            return this.tail.value;
        } else {
            return nil;
        }
    }
}